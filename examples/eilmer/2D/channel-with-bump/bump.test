# bump.test
# Functional test for inviscid flow in a channel.
#
# This exercises quite a few of the basic functions of the block-marching code.
#
# PJ, 17-Sep-2013 Eilmer3 version
#     17-Nov-2015 Eilmer4 port
# Note that later tests below depend on earlier ones,
# just as we shouldn't do.  Should fix this some day.

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test prepare-gas-model {Prepare gas model.} -body {
    exec prep-gas ideal-air.inp ideal-air-gas-model.lua > LOGFILE_PREP_GAS
} -result {} -returnCodes {0}

test run-preprocessor {Run the preprocessing stage.} -body {
    exec e4shared --prep --job=bump --verbosity=1 > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the block-marching simulation code.} -body {
    exec e4shared --run --job=bump --verbosity=1 --max-cpus=4 > LOGFILE_MAIN
} -result {} -returnCodes {0}

test exit-flow {The shock-processed flow near the outlet of the duct.} -body {
    set a 0; set M 0; set p 0; set T 0
    if { [catch {exec e4shared --post --job=bump --tindx-plot=last --add-vars="mach" --probe=2.921,0.364,0.0 } results] } {
        puts "Flow-field-probing command failed"
        puts $results
    } else {
	# puts $results
	# Skip through the output, looking for the interesting data.
	# There will be a header line, followed by the actual data values.
	set found 0
	foreach line [split $results "\n"] {
	    if {[string first "pos.x" $line] >= 0} {
		set found 1
		continue; # skip to following line
	    }
	    if {$found} {
		set tokens [split [string trim $line]]
		# puts "tokens= $tokens"
		set a [lindex $tokens 9]
		set M [lindex $tokens 20]
		set p [lindex $tokens 8]
		set T [lindex $tokens 19]
		break; # We have our data so stop looking.
	    }
	}
    }
    # The shock wave angle should be within 1 degree of expected
    # and average deviation of points should be less than 2mm. 
    list [expr abs($a - 349.3) < 1.0] [expr abs($M - 1.52) < 0.05] \
	[expr abs($p - 121000) < 1000] [expr abs($T - 303.5) < 1.0]
} -result {1 1 1 1}

cleanupTests

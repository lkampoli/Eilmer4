# reactor.test
#
# This exercises the finite-rate chemistry coupled to the gas-dynamics.
#
# PJ, 2018-04-21 Eilmer4 port

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test prep-chem {Gas and chemistry preparation stage.} -body {
    exec ./prep-chem.sh > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the shared-memory simulation code.} -body {
    exec ./run-simulation.sh > LOGFILE_RUN
} -result {} -returnCodes {0}

test number-of-steps {The main simulation takes a certain number of steps.} -body {
    set fp [open LOGFILE_RUN r]
    set contents [read $fp]
    close $fp
    set final_steps 0
    foreach line [split $contents "\n"] {
	if {[string first "final-t=" $line] >= 0} {
	    set final_steps [lindex [split $line] 1]
	}
    }
    list [expr abs($final_steps - 201) < 3]
} -result {1}

test reaction-products {Recombination should have occurred over time.} -body {
    # The following values were extracted from the script results.
    set p_ref 1.494e5
    set T_ref 6381.7
    set massf_N2_ref 0.8760
    set massf_N_ref 0.1240
    # Pick up the history file and pull a few values from the last line of data.
    set fp [open hist/reactor-blk-0-cell-0.dat.0 r]
    set results [string trim [read $fp]]
    close $fp
    set line_of_data [string trim [lindex [split $results "\n"] end]]
    # puts $line_of_data
    set items [split $line_of_data]
    set p [lindex $items 9]; # remember that index starts at 0
    set T [lindex $items 22]
    set massf_N2 [lindex $items 18]
    set massf_N [lindex $items 19]
    # puts "p=$p T=$T massf N=$massf_N N2=$massf_N2"
    
    # The differences with respect to the reference values should be small.
    list [expr abs($p - $p_ref)/$p_ref < 1.0e-3] \
        [expr abs($T - $T_ref)/$T_ref < 1.0e-3] \
        [expr abs($massf_N - $massf_N_ref)/$massf_N_ref < 1.0e-3] \
        [expr abs($massf_N2 - $massf_N2_ref)/$massf_N2_ref < 1.0e-3]
} -result {1 1 1 1}

cleanupTests

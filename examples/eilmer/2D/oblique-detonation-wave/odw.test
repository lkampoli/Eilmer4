# odw.test
# Functional test for the Powers and Aslam oblique detonation wave.
#
# This exercises quite a few of the basic functions of the 2D code
# plus the generic connection to a custom reacting gas.
#
# PAJ, 2017-01-08 (ported from cone20.test)
#
# Can be run manually with the command:
# $ tclsh odw.test
#
# Note that later tests below depend on earlier ones,
# just as we shouldn't do.  Should fix this some day.

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test run-preprocessor {Run the preprocessing stage.} -body {
    exec e4shared --prep --job=odw > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the shared-memory simulation code.} -body {
    exec e4shared --run --job=odw > LOGFILE_MAIN
} -result {} -returnCodes {0}

test number-of-steps {The main simulation takes a certain number of steps.} -body {
    set fp [open LOGFILE_MAIN r]
    set contents [read $fp]
    close $fp
    set final_steps 0
    foreach line [split $contents "\n"] {
	if {[string first "final-t=" $line] >= 0} {
	    set final_steps [lindex [split $line] 1]
	}
    }
    list [expr abs($final_steps - 2104) < 3]
} -result {1}

test shock-angle {The shock angle should be close to 45 degrees.} -body {
    set shock_angle 0
    set average_deviation 0
    if { [catch {exec e4shared --custom-post --script-file=estimate_shock_angle.lua} results] } {
        puts "specialized postprocessing command failed"
        puts $results
    } else {
	foreach line [split $results "\n"] {
	    if {[string first shock_angle_deg $line] >= 0} {
		set shock_angle [lindex [split $line] 1]
	    }
	    if {[string first average_deviation_metres $line] >= 0} {
		set average_deviation [lindex [split $line] 1]
	    }
	}
    }
    # The shock wave angle should be within 1 degree of expected
    # and average deviation of points should be less than 2mm. 
    list [expr abs($shock_angle - 45.0) < 0.5] [expr $average_deviation < 0.002]
} -result {1 1}

cleanupTests

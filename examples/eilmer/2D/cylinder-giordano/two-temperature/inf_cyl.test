# inf_cyl.test
# Functional test for 2D, inviscid flow in thermal nonequilibrium.
#
# This exercises quite a few of the basic functions of the 2D code
# plus Rowan's two-temperature nitrogen model.
#
# PJ, 13-Jan-2011
#     01-Aug-2017 Ported to Eilmer4
#     2019-05-07 Increase tolerances to accommodate no-ghost-cell BC. 

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test run-preprocessor {Run the preprocessing stage.} -body {
    exec e4shared --prep --job=inf_cyl > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the mpi simulation code.} -body {
    exec e4shared --run --job=inf_cyl --max-cpus=4  > LOGFILE_MAIN
} -result {} -returnCodes {0}

test number-of-steps {The main simulation takes a certain number of steps.} -body {
    set fp [open LOGFILE_MAIN r]
    set contents [read $fp]
    close $fp
    set final_steps 0
    foreach line [split $contents "\n"] {
	if {[string first "final-t=" $line] >= 0} {
	    set final_steps [lindex [split $line] 1]
	}
    }
    expr abs($final_steps - 7124) < 50
} -result {1}

test run-postprocessor {Run the postprocessing stage.} -body {
    if { [catch {exec e4shared --job=inf_cyl --post \
		     --slice-list=0,:,0,0 \
		     --output-file=stag-prof-50Pa-Blackman.data \
		     > LOGFILE_POST} results] } {
        puts "e4shared postprocessing command failed"
        puts $results
    }
    exec tail -1 stag-prof-50Pa-Blackman.data > values.txt
    set fp [open values.txt r]
    set contents [read $fp]
    close $fp
    set T_tr [lindex [split [string trim $contents]] 20]
    set T_v [lindex [split [string trim $contents]] 22]
    list [expr abs($T_tr - 2536.4) < 20.0] \
         [expr abs($T_v - 2349.0) < 60.0]

} -result {1 1}

cleanupTests
